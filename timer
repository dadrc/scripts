#!/usr/bin/zsh

# dirty little helper
function setTime() {
	eval $1=$match[1]
	valid=true
}

# print seconds in human-readable way
function prettyPrint() {
	# for readability
	local seconds=$1
	# normalize seconds
	local minutes=$(($seconds / 60))
	seconds=$(($seconds % 60))
	# normalize minutes
	local hours=$(($minutes / 60))
	minutes=$(($minutes % 60))
	if [[ $seconds -lt  10 ]] seconds=${(l:2::0:)${seconds}}
	if [[ $minutes -lt  10 ]] minutes=${(l:2::0:)${minutes}}
	echo $hours:$minutes:$seconds
}

# catch missing parameter
if [[ -z $1 ]]; then
	echo "Usage: ${0##*/} <hours>h<minutes>m<seconds>s [<description>]"
	exit 1
fi

# initialize stuff
hours=0
minutes=0
seconds=0
valid=false
if [[ -n $2 ]] timer=" ${(qq)2}"

# parse timestamp
[[ "$1" =~ "([0-9]+)h" ]] && setTime "hours"
[[ "$1" =~ "([0-9]+)m" ]] && setTime "minutes"
[[ "$1" =~ "([0-9]+)s" ]] && setTime "seconds"

# at least one match was successful
if $valid; then
	# calculate seconds
	minutes=$(($hours * 60 + $minutes))
	seconds=$(($minutes * 60 + $seconds))
	echo "Starting timer: $(prettyPrint $seconds)."
	# offset 
	seconds=$(($seconds + 1))
	# sleep for given time while printing progress
	while (( seconds-- > 1 )) {
		print -Pn "\r\e[0K$(prettyPrint $seconds) remaining"
		sleep 1
	}
	print -Pn "\r\e[0K$(prettyPrint $seconds) remaining"
	# see initialization
	print -P "\r\e[0KTimer${timer} finished on $(date +%d.%m.%Y) at $(date +%H:%M:%S)."
	# notify
	notify-send --icon=clock "Timer finished" "$2"
else
	echo "Invalid timestamp, exiting."
	exit 1
fi
exit 0
